program gridgen_otype
  implicit none

  ! Definition of double precision
  integer, parameter  :: DP = selected_real_kind(15,307)

  ! Definition of mathematical constant PI
  real(DP), parameter :: pi = asin(1.0_DP)*2.0_DP

  ! Definition of the inner radius
  real(DP) :: dinnerRadius = 0.1_DP

  ! Definition of the outer radius
  real(DP) :: douterRadius = 1.0_DP

  ! Definition of the number of segments
  integer :: nsegments = 6

  ! Definition of the number of outer layers
  integer :: nlayers = 1

  ! Definition of output filename
  character(len=80) :: cfilename = "grid"

  ! Definition of output format
  character(LEN=*), parameter :: cFormat = '(F32.15)'
 
  ! local variables
  character(len=80) :: cbuffer
  character(len=32) :: cbuffer1,cbuffer2,cbuffer3,cbuffer4
  real(DP) :: x,y,r,phi
  integer :: i,j,ivt

  !-----------------------------------------------------------------------------
  ! Get command line arguments
  !-----------------------------------------------------------------------------
  
  do i = 1, command_argument_count()

    call get_command_argument(i, cbuffer)
    select case(cbuffer)
    case('-R0','-innerradius')
      call get_command_argument(i+1, cbuffer)
      read(cbuffer,*) dinnerRadius
      
    case('-R1','-outerradius')
      call get_command_argument(i+1, cbuffer)
      read(cbuffer,*) douterRadius

    case('-S','-segments')
      call get_command_argument(i+1, cbuffer)
      read(cbuffer,*) nsegments

    case('-L','-layers')
      call get_command_argument(i+1, cbuffer)
      read(cbuffer,*) nlayers

    case('-F','-filename')
      call get_command_argument(i+1, cbuffer)
      read(cbuffer,*) cfilename
    end select
  end do

  write(*,'(A)') 'Generating annular grid'
  write(*,'(A)') '-----------------------'
  write(cbuffer, *) dinnerRadius
  write(*,'(A,T45,A)') 'inner radius:',trim(adjustl(cbuffer))
  write(cbuffer, *) douterRadius
  write(*,'(A,T45,A)') 'outer radius:',trim(adjustl(cbuffer))
  write(cbuffer, *) nlayers
  write(*,'(A,T45,A)') 'number of layers in radial direction:',trim(adjustl(cbuffer))
  write(cbuffer, *) nsegments
  write(*,'(A,T45,A)') 'number of segments in angular direction:',trim(adjustl(cbuffer))
  write(*,'(A,T45,A)') 'name of output file: ',trim(adjustl(cfilename))
  
  !-----------------------------------------------------------------------------
  ! Generate PRM-file
  !-----------------------------------------------------------------------------

  open(unit=100, file=trim(adjustl(cfilename))//'.prm')

  write(100,'(A)') 'NBCT'
  write(100,'(A)') '1'
  write(100,'(A)') 'IBCT'
  write(100,'(A)') '1 '
  write(100,'(A)') 'NCOMP'
  write(100,'(A)') '1'
  write(100,'(A)') 'ITYP NSPLINE NPAR'
  write(100,'(A)') '2 1 3 '
  write(100,'(A)') 'PARAMETERS'
  write(cbuffer1, cFormat) 0.0
  write(100,'(A)') trim(adjustl(cbuffer1))//' '//trim(adjustl(cbuffer1))
  write(cbuffer2, cFormat) douterRadius
  write(100,'(A)') trim(adjustl(cbuffer2))//' '//trim(adjustl(cbuffer1))
  write(cbuffer2, cFormat) 2.0_DP*pi
  write(100,'(A)') trim(adjustl(cbuffer1))//' '//trim(adjustl(cbuffer2))
  
  close(100)
  

  !-----------------------------------------------------------------------------
  ! Generate TRI-file
  !-----------------------------------------------------------------------------

  open(unit=100, file=trim(adjustl(cfilename))//'.tri')

  write(100,'(A)') 'Coarse mesh 2D'
  write(100,'(A)') 'Generated by gridgen-otype'
  
  write(cbuffer1, '(I)') nsegments*nlayers
  write(cbuffer2, '(I)') nsegments*nlayers+1

  write(100,'(A)') trim(adjustl(cbuffer1))//' '//trim(adjustl(cbuffer2))//' 0 4 1  NEL NVT NMT NVE NBCT'

  !-----------------------------------------------------------------------------
  write(100,'(A)') 'DCORVG'
  !-----------------------------------------------------------------------------

  ! Write vertex at origin
  write(cbuffer1, cFormat) 0.0
  write(100,'(A)') trim(adjustl(cbuffer1))//' '//trim(adjustl(cbuffer1))

  ! Write interior vertices
  do i = 1, nsegments
    do j = 1, nlayers-1

      ! Compute radius
      r = dinnerRadius + j*(douterRadius-dinnerRadius)/nlayers
      
      ! Compute angle
      phi = 2*(i-1)*pi/nsegments

      write(cbuffer1, cFormat) r * cos(phi)
      write(cbuffer2, cFormat) r * sin(phi)

      write(100,'(A)') trim(adjustl(cbuffer1))//' '//trim(adjustl(cbuffer2))
    end do

    ! Write parameter for boundary vertex
    r = real(i-1,DP)/real(nsegments,DP)
    write(cbuffer1, cFormat) r
    write(cbuffer2, cFormat) 0.0
    write(100,'(A)') trim(adjustl(cbuffer1))//' '//trim(adjustl(cbuffer2))
  end do

  !-----------------------------------------------------------------------------
  write(100,'(A)') 'KVERT'
  !-----------------------------------------------------------------------------

  do i = 1, nsegments
    do j = 1, nlayers-1

      ! Compute base vertex number
      ivt = j-1 + nlayers*(i-1)
      
      write(cbuffer1, '(I)') mod(ivt,     nsegments*nlayers)+2
      write(cbuffer2, '(I)') mod(ivt+1,   nsegments*nlayers)+2
      write(cbuffer3, '(I)') mod(ivt+nlayers+1, nsegments*nlayers)+2
      write(cbuffer4, '(I)') mod(ivt+nlayers,   nsegments*nlayers)+2
      
      write(100,'(A)') trim(adjustl(cbuffer1))//' '//trim(adjustl(cbuffer2))//' '//&
                       trim(adjustl(cbuffer3))//' '//trim(adjustl(cbuffer4))
    end do
  end do

  do i = 1, nsegments
    
    write(cbuffer1, '(I)') mod(nlayers*(i-1), nsegments*nlayers)+2
    write(cbuffer2, '(I)') mod(nlayers*i,     nsegments*nlayers)+2
    
    write(100,'(A)') trim(adjustl(cbuffer1))//' '//trim(adjustl(cbuffer2))//' 1 0'
    
  end do
  
  !-----------------------------------------------------------------------------
  write(100,'(A)') 'KNPR'
  !-----------------------------------------------------------------------------
  
  ! Write nodal property for vertex at origin
  write(100,'(A)') '0'

  do i = 1, nsegments
    ! Write nodal property for interior vertex
    do j = 1, nlayers-1
      write(100,'(A)') '0'
    end do
    ! Write nodal property for boundary vertex
    write(100,'(A)') '1'
  end do
  
  !-----------------------------------------------------------------------------
  write(100,'(A)') 'KMM'
  !-----------------------------------------------------------------------------
  
  write(cbuffer1, '(I)') nlayers+1
  write(cbuffer2, '(I)') nlayers*nsegments+1

  write(100,'(A)') trim(adjustl(cbuffer1))//' '//trim(adjustl(cbuffer2))
  
  close(100)

end program gridgen_otype
